# NOTE: Everything below is not generated by upstream, but by @ErichDonGubler.
let ctrl_b_handler = "
def must_replace [what: string, find: string, replace: string] {
	let original = $in
	let replaced = $original | str replace $find $replace
	if $replaced == $original {
		error make {
			msg: $'failed to replace string in ($what)'
			label: {
				text: 'unable to find this text'
				span: (metadata $find).span
			}
		}
	}
	$replaced
}

$env.config = (
	$env.config | upsert keybindings (
		$env.config.keybindings
		| append {
			name: atuin
			modifier: control
			keycode: char_b
			mode: [emacs, vi_normal, vi_insert]
			event: {
				send: ExecuteHostCommand
				cmd: (
					alias must_replace = must_replace 'command generated by `_atuin_search_cmd`';
					_atuin_search_cmd
						| must_replace 'ATUIN_QUERY: (commandline)' 'ATUIN_QUERY: ""'
						| must_replace 'commandline edit' 'commandline edit --insert'
				)
			}
		}
	)
)
"

const cache_file = ($nu.cache-dir | path join 'atuin' 'init.nu')

mkdir ($cache_file | path dirname)
atuin init nu --disable-up-arrow | save -f $cache_file
echo $ctrl_b_handler | save --append $cache_file

source $cache_file
